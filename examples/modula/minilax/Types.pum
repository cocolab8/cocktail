TRAFO Types PUBLIC

Reduce			/* return type without any ref levels		*/

ReduceToRef		/* return type with ref level 1			*/

Reduce1			/* return type with 1 ref level removed		*/

RefLevel		/* return number of ref levels of a type	*/

IsSimpleType		/* check whether a type is simple		*/

IsCompatible		/* check whether two types are compatible	*/

IsAssignmentCompatible	/* check whether two types are			*/
			/* assignment compatible			*/

ResultType		/* return the type of the result of		*/
			/* applying an operator to two operands		*/

CheckParams		/* check a formal list of parameters		*/
			/* against an actual list of parameters		*/

GetElementType		/* return the type of the elements of		*/
			/* an array type				*/

TypeSize		/* return the number of bytes used for		*/
			/* the internal representation of an		*/
			/* object of a certain type			*/

Coerce			/* returns the coercion necessary to convert	*/
			/* an object of type 't1' to type 't2'		*/

EXTERN Message Error

GLOBAL {

FROM Position	IMPORT tPosition;
FROM Errors	IMPORT Message, Error;
FROM Tree	IMPORT
   tTree	, Array		, Ref		, NoType	,
   Plus		, Times		, Less		, Not		,
   mBoolean	, mNoType	, mNoCoercion	;

VAR nBoolean, nNoType, nNoCoercion	: tTree;
}

BEGIN {
   nBoolean	:= mBoolean	();
   nNoType	:= mNoType	();
   nNoCoercion	:= mNoCoercion	();
}

FUNCTION Reduce (Type) Type
   Ref (t)	RETURN Reduce (t) :- .
   t		RETURN t :- .

FUNCTION ReduceToRef (Type) Type
   Ref (t:Ref)	RETURN ReduceToRef (t) :- .
   t:Ref	RETURN t :- .
   t		RETURN t :- .

FUNCTION Reduce1 (Type) Type
   Ref (t)	RETURN t :- .
   t		RETURN t :- .

FUNCTION RefLevel (Type) INTEGER
   Ref (t)	RETURN RefLevel (t) + 1 :- .
   _		RETURN 0 :- .

PREDICATE IsSimpleType (Type)
   Array	:-  FAIL; .
   _		:- .

PREDICATE IsCompatible (Type, Type)
   Integer	, Integer	:- .
   Real		, Real		:- .
   Boolean	, Boolean	:- .
   Array (t1, Lwb, Upb, _), Array (t2, Lwb, Upb, _) ;
   Ref (t1)	, t2		;
   t1		, Ref (t2)	:-  IsCompatible (t1, t2); .
   NoType	, _		:- .
   _		, NoType	:- .
   ErrorType	, _		:- .
   _		, ErrorType	:- .

PREDICATE IsAssignmentCompatible (Type, Type)
   Integer	, Integer	:- .
   Real		, Real		:- .
   Real		, Integer	:- .
   Boolean	, Boolean	:- .
   Ref (t1)	, t2		;
   t1		, Ref (t2)	:-  IsAssignmentCompatible (t1, t2); .
   NoType	, _		:- .
   _		, NoType	:- .
   ErrorType	, _		:- .
   _		, ErrorType	:- .

FUNCTION ResultType (Type, Type, INTEGER) Type	EXTERN Plus Times Less Not nBoolean;
   t:Integer	, Integer	, (Plus)	RETURN t	:- .
   t:Real	, Real		, (Plus)	RETURN t	:- .
   t:Integer	, Integer	, (Times)	RETURN t	:- .
   t:Real	, Real		, (Times)	RETURN t	:- .
   Integer	, Integer	, (Less)	RETURN nBoolean	:- .
   Real		, Real		, (Less)	RETURN nBoolean	:- .
   t:Boolean	, Boolean	, (Less)	RETURN t	:- .
   t:Boolean	, _		, (Not)		RETURN t	:- .
   Ref (t1)	, t2		, o		;
   t1		, Ref (t2)	, o		RETURN ResultType (t1, t2, o) :- .
   t:NoType	, _		, _		RETURN t	:- .
   _		, t:NoType	, _		RETURN t	:- .
   ErrorType	, _		, _		RETURN NoType	:- .
   _		, ErrorType	, _		RETURN NoType	:- .
   ..						RETURN ErrorType:- .

PROCEDURE CheckParams (Actuals, Formals)
   NoActual	, NoFormal	:- .
   NoActual (Pos), _		:-
      Message ("too few actual parameters", Error, Pos); .
   Actual (_, Expr (Pos, ..)), NoFormal :-
      Message ("too many actual parameters", Error, Pos); .

/* alternative 1 */

   Actual (NextA, Expr (Pos, TypeA, ..)), Formal (_, _, NextF, _, _, TypeF) :-
      IF NOT IsCompatible (TypeA, TypeF) THEN
	 Message ("parameter type incompatible", Error, Pos);
      END
      IF NOT (RefLevel (TypeF) - 1 <= RefLevel (TypeA)) THEN
	 Message ("variable required", Error, Pos);
      END
      CheckParams (NextA, NextF); .

/* alternative 2 */

   Actual (NextA, Expr (Pos, TypeA, ..)), Formal (_, _, NextF, _, _, TypeF) :-
      CONDITION NOT IsCompatible (TypeA, TypeF);
      Message ("parameter type incompatible", Error, Pos);
      REJECT; .

   Actual (NextA, Expr (Pos, TypeA, ..)), Formal (_, _, NextF, _, _, TypeF) :-
      CONDITION NOT (RefLevel (TypeF) - 1 <= RefLevel (TypeA));
      Message ("variable required", Error, Pos);
      REJECT; .

   Actual (NextA, Expr (Pos, TypeA, ..)), Formal (_, _, NextF, _, _, TypeF) :-
      CheckParams (NextA, NextF); .

/* alternative 3 */

   Actual (NextA, Expr (Pos, TypeA, ..)), Formal (_, _, NextF, _, _, TypeF) :-
      CheckCompatible (Pos, TypeA, TypeF);
      CheckRefLevel (Pos, TypeA, TypeF);
      CheckParams (NextA, NextF); .

PROCEDURE CheckCompatible (tPosition, Type, Type)
   _	, t1	, t2	:- IsCompatible (t1, t2); .
   Pos	, ..		:- Message ("parameter type incompatible", Error, Pos);.

PROCEDURE CheckRefLevel (tPosition, Type, Type)
   _	, t1	, t2	:- CONDITION RefLevel (t2) - 1 <= RefLevel (t1); .
   Pos	, ..		:- Message ("variable required", Error, Pos); .

FUNCTION GetElementType (Type) Type
   Array (t, ..)	RETURN t :- .
   _			RETURN NoType :- .

FUNCTION TypeSize (Type) INTEGER
   Array (t, Lwb, Upb, _)	RETURN (Upb - Lwb + 1) * TypeSize (t) :- .
   _				RETURN 1 :- .

FUNCTION Coerce (t1: Type, t2: Type) Coercions	EXTERN nNoCoercion;
   Ref (T1)	, Ref (T2)	RETURN Coerce (T1, T2) :- .
   Integer	, Real		RETURN IntToReal (nNoCoercion) :- .
   Ref (T1)	, T2		RETURN Content (Coerce (T1, T2)) :- .
   ..				RETURN nNoCoercion :- .
