TRAFO ICode PUBLIC Code

EXTERN
   ADD BoolType CHK ENT Emit EmitReal FJP FLT FalseCode INV IXA IntType JMP JSR
   LDA LDC LDI LES MST MUL REA RET RealType STI SUB TrueCode TypeSize WRI
   NewLabel DeclLabel GetLevel Times Plus Less rtrue rfalse

GLOBAL {
# include "Tree.h"
# include "Types.h"
# include "ICodeInt.h"

# define GetLevel(Env) (Env->Proc.Level + 1)
}

PROCEDURE Code (t: Tree)

MiniLax	(Proc) :-
	Code (Proc);
	.
Proc (Next := Next, Decls := Decls, Stats := Stats, Label := Label,
      ParSize := ParSize, DataSize := DataSize) :-
{	if (Label == 0) Label = NewLabel (); };
	DeclLabel (Label);
	Emit (ENT, DataSize - ParSize, 0);
	Code (Stats);
	Emit (RET, 0, 0);
	Code (Decls);
	Code (Next);
	.
Var (Next := Next) :-
	Code (Next);
	.
Assign (_, Next, Adr, Expr, _) :-
	Code (Adr); Code (Adr::Co);
	Code (Expr); Code (Expr::Co);
	Emit (STI, 0, 0);
	Code (Next);
	.
Call (Env, Next, Actuals, _, _, Proc (Level := Level, Label := Label, ParSize := ParSize)) :-
{	if (Label == 0) Label = NewLabel (); };
	Emit (MST, GetLevel (Env) - Level, 0);
	Code (Actuals);
	Emit (JSR, ParSize - 3, Label);
	Code (Next);
	.
If (_, Next, Expr, Then, Else) :-
	Label1: int, Label2: int;
	Label1 := NewLabel ();
	Label2 := NewLabel ();
	Code (Expr); Code (Expr::Co);
	Emit (FJP, Label1, 0);
	Code (Then);
	Emit (JMP, Label2, 0);
	DeclLabel (Label1);
	Code (Else);
	DeclLabel (Label2);
	Code (Next);
	.
While (_, Next, Expr, Stats) :-
	Label1: int, Label2: int;
	Label1 := NewLabel ();
	Label2 := NewLabel ();
	Emit (JMP, Label2, 0);
	DeclLabel (Label1);
	Code (Stats);
	DeclLabel (Label2);
	Code (Expr); Code (Expr::Co);
	Emit (INV, 0, 0);
	Emit (FJP, Label1, 0);
	Code (Next);
	.
Read (_, Next, Adr, TypeCode) :-
	Code (Adr); Code (Adr::Co);
	Emit (REA, TypeCode, 0);
	Emit (STI, 0, 0);
	Code (Next);
	.
Write (_, Next, Expr, TypeCode) :-
	Code (Expr); Code (Expr::Co);
	Emit (WRI, TypeCode, 0);
	Code (Next);
	.
Actual (_, Next, Expr) :-
	Code (Expr); Code (Expr::Co);
	Code (Next);
	.
Binary (Lop := Lop, Rop := Rop, Operator := (Times), TypeCode := TypeCode) :-
	Code (Lop); Code (Lop::Co);
	Code (Rop); Code (Rop::Co);
	Emit (MUL, TypeCode, 0);
	.
Binary (Lop := Lop, Rop := Rop, Operator := (Plus), TypeCode := TypeCode) :-
	Code (Lop); Code (Lop::Co);
	Code (Rop); Code (Rop::Co);
	Emit (ADD, TypeCode, 0);
	.
Binary (Lop := Lop, Rop := Rop, Operator := (Less), TypeCode := TypeCode) :-
	Code (Lop); Code (Lop::Co);
	Code (Rop); Code (Rop::Co);
	Emit (LES, TypeCode, 0);
	.
Unary (Expr := Expr) :-
	Code (Expr); Code (Expr::Co);
	Emit (INV, 0, 0);
	.
IntConst (Value := Value) :-
	Emit (LDC, IntType, Value);
	.
RealConst (Value := Value) :-
	EmitReal (LDC, RealType, Value);
	.
BoolConst (Value := (rtrue)) :-
	Emit (LDC, BoolType, TrueCode);
	.
BoolConst (Value := (rfalse)) :-
	Emit (LDC, BoolType, FalseCode);
	.
Index (Adr := Adr, Expr := Expr, Type2 := Array (Type, Lwb, Upb, _)) :-
	Code (Adr); Code (Adr::Co);
	Code (Expr); Code (Expr::Co);
	Emit (CHK, Lwb, Upb);
	Emit (LDC, IntType, Lwb);
	Emit (SUB, IntType, 0);
	Emit (IXA, TypeSize (Type), 0);
	.
Ident (Env := Env, Object := Var (Level := Level, OffsetIn := Offset)) :-
	Emit (LDA, GetLevel (Env) - Level, Offset);
	.
Ident (Env := Env, Object := Formal (Level := Level, OffsetIn := Offset)) :-
	Emit (LDA, GetLevel (Env) - Level, Offset);
	.
Content (Next) :-
	Emit (LDI, 0, 0);
	Code (Next);
	.
IntToReal (Next) :-
	Emit (FLT, 0, 0);
	Code (Next);
	.
