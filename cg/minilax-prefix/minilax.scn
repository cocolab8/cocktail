EXPORT  {
# include "Idents.h"
# include "Position.h"

INSERT tScanAttribute
}

GLOBAL	{
# include <stdlib.h>
# include <math.h>
# include "Errors.h"

INSERT ErrorAttribute
}

DEFAULT	{
   MessageI ("illegal character", xxError, Attribute.Position, xxCharacter,
		TokenPtr);
}

EOF	{
   if (yyStartState == Comment)
      Message ("unclosed comment", xxError, Attribute.Position);
}

DEFINE	digit	= {0-9} .
	letter	= {a-z A-Z} .

START	Comment

RULE
	  "(*"	:- {yyStart (Comment);}
#Comment# "*)"	:- {yyStart (STD);}
#Comment# "*" | - {*\t\n} + :- {}

#STD# digit +	: {Attribute.IntegerConst.Integer = (int) atoi (TokenPtr);
		   return IntegerConst;}

#STD# digit * "." digit + (E {+\-} ? digit +) ?
		: {Attribute.RealConst.Real = (float) atof (TokenPtr);
		   return RealConst;}

INSERT RULES #STD#

#STD# letter (letter | digit) *
		: {Attribute.Ident.Ident = MakeIdent (TokenPtr, TokenLength);
		   return Ident;}
