(* $Id: Heap.mi,v 1.11 2008/09/25 21:04:15 grosch Exp $ *)

(*
 * $Log: Heap.mi,v $
 * Revision 1.11  2008/09/25 21:04:15  grosch
 * replaced operation PTRINC by arithmetic with type ADDRESS
 *
 * Revision 1.10  2008/09/12 14:20:59  grosch
 * added module Pointers in order to handle pointers on 64 bit platforms
 *
 * Revision 1.9  1997/05/27 11:57:56  grosch
 * truncation of filenames to 8.3 format
 *
 * Revision 1.8  1994/12/30 19:39:43  grosch
 * adaption to HP cc
 *
 * Revision 1.7  1994/06/13  09:41:43  grosch
 * fix alignment for DEC Alpha
 *
 * Revision 1.6  1993/08/18  15:06:51  grosch
 * rename System and Memory to rSystem and rMemory
 *
 * Revision 1.5  1991/11/21  14:33:17  grosch
 * new version of RCS on SPARC
 *
 * Revision 1.4  90/03/02  17:36:09  grosch
 * automized handling of machine independent alignment
 * 
 * Revision 1.3  90/02/28  22:07:01  grosch
 * comment for alignment on SPARC
 * 
 * Revision 1.2  90/02/12  10:53:40  grosch
 * introduced a machine dependent variant for MIPS
 * 
 * Revision 1.1  89/07/13  11:27:01  grosch
 * reset pointers to NIL in Free
 * 
 * Revision 1.0  88/10/04  11:46:56  grosch
 * Initial revision
 * 
 *)

(* Ich, Doktor Josef Grosch, Informatiker, 2.9.1988 *)

IMPLEMENTATION MODULE Heap;

FROM SYSTEM	IMPORT ADDRESS, ADR, TSIZE;
FROM Pointers	IMPORT PTRDIFF;
FROM General	IMPORT MaxAlign, AlignMask;

IMPORT rMemory;

CONST
   PoolSize		= 10240	;

TYPE
   tBlockPtr		= POINTER TO tBlock;
   tBlock		= RECORD
			     Successor	: tBlockPtr	;
			     Block	: ARRAY [1..PoolSize] OF CHAR;
			  END;

VAR
   BlockList	: tBlockPtr;
   PoolFreePtr	: ADDRESS;
   PoolEndPtr	: ADDRESS;

(* Returns a pointer to dynamically allocated	*)
(* space of size 'ByteCount' bytes.		*)

PROCEDURE Alloc	(ByteCount: LONGINT): ADDRESS;
VAR
   BlockPtr	: tBlockPtr;
   PrevFreePtr	: ADDRESS;
BEGIN
   ByteCount := LONGINT (BITSET (ByteCount + MaxAlign - 1) * AlignMask);
   IF PTRDIFF (PoolEndPtr, PoolFreePtr) < ByteCount THEN
      BlockPtr	  := BlockList;
      BlockList	  := rMemory.Alloc (TSIZE (tBlock));
      BlockList^.Successor := BlockPtr;
      PoolFreePtr := ADR (BlockList^.Block);
      PoolEndPtr  := PoolFreePtr + PoolSize;
      INC (HeapUsed, PoolSize);
   END;
   PrevFreePtr := PoolFreePtr;
   INC (PoolFreePtr, CARDINAL (ByteCount));
   RETURN PrevFreePtr;
END Alloc;

(* The complete space allocated for the heap	*)
(* is released.					*)

PROCEDURE Free;
VAR BlockPtr	: tBlockPtr;
BEGIN
   WHILE BlockList # NIL DO
      BlockPtr	:= BlockList;
      BlockList	:= BlockList^.Successor;
      rMemory.Free (TSIZE (tBlock), BlockPtr);
   END;
   PoolFreePtr	:= NIL;
   PoolEndPtr	:= NIL;
   HeapUsed	:= 0;
END Free;

BEGIN
   BlockList	:= NIL;
   Free;
END Heap.
